plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    namespace 'com.example.homework2'
    compileSdk targetAndroidSdk
    defaultConfig {
        applicationId "com.example.homework2"

        minSdk minAndroidSdk
        targetSdk targetAndroidSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keys.properties")))
    buildTypes.each {
        it.buildConfigField 'String', 'API_ENDPOINT', keystoreProperties['baseUrl']
        it.buildConfigField 'String', 'API_KEY', keystoreProperties['apiKey']
        it.buildConfigField 'int', 'ID', keystoreProperties['myId']
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }

}

dependencies {
    // region Network
    implementation Libs.Network.Retrofit.Retrofit
    implementation Libs.Network.Retrofit.SerialiazationConverter
    implementation Libs.Network.SerialiazationJson
    implementation Libs.Network.OkHttp.LoggingInterceptor
    //endregion

    //region Elm
    implementation Libs.Elmslie.Android
    //endregion

    //region cicerone
    implementation Libs.Cicerone
    //endregion

    //region Room
    implementation Libs.Android.Room.Runtime
    annotationProcessor Libs.Android.Room.Compiler
    kapt Libs.Android.Room.Compiler
    implementation Libs.Android.Room.Ktx
    //endregion

    //region coroutine
    implementation Libs.Coroutine.Core
    implementation Libs.Coroutine.Android
    implementation Libs.Android.Lifecycle
    //endregion

    //region dagger
    implementation Libs.Dagger.Dagger
    kapt Libs.Dagger.Compiler
    //end region

    //region utils
    implementation Libs.Utils.Timber
    //end region

    implementation Libs.Android.ConstraintLayout
    implementation Libs.Android.CoreKtx
    implementation Libs.Android.AppCompat
    implementation Libs.Android.Material

    implementation project(":common")
    implementation project(":profile")
    implementation project(":messenger")
    implementation project(":channels")
    implementation project(":people")

    androidTestImplementation Libs.AndroidTesting.JUnit
    androidTestImplementation Libs.AndroidTesting.Kaspresso
    androidTestImplementation Libs.AndroidTesting.MockWebServer
    androidTestImplementation Libs.AndroidTesting.Runner
    androidTestUtil Libs.AndroidTesting.Orchestrator
    debugImplementation Libs.AndroidTesting.Monitor
}
